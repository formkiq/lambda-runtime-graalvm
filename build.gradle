buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.27.1"
    }
}

plugins {
  id "com.github.spotbugs" version "4.5.0"
}

allprojects {

	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'checkstyle'
	apply plugin: 'signing'
	apply plugin: 'com.diffplug.gradle.spotless'

	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

    group 'com.formkiq'
    version '2.0'

	spotless {
	  java {
	    googleJavaFormat()
	  }
	}
	
	spotbugsMain {
	    reports {
	        html {
	            enabled = true
	            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
	            stylesheet = 'fancy-hist.xsl'
	        }
	    }
	}
  
	checkstyle {
		toolVersion '8.29'
		configFile file("config/checkstyle/checkstyle.xml")
		configProperties = [project_loc: "${projectDir}"]
  	}
  
    tasks.withType(Checkstyle).each { checkstyleTask ->
	    checkstyleTask.doLast {
	        reports.all { report ->
	            def outputFile = report.destination
	            if (outputFile.exists() && outputFile.text.contains("<error ")) {
	                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
	            }
	        }
	    }
	}
	
	repositories {
	    jcenter()
	}
    
    java {
	    withJavadocJar()
	    withSourcesJar()
	}
    
	javadoc {
	    if(JavaVersion.current().isJava9Compatible()) {
	        options.addBooleanOption('html5', true)
	    }
	}
	
	artifacts {
	    archives jar
		
	    archives javadocJar
	    archives sourcesJar
	}
}
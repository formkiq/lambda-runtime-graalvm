plugins {
    id 'java-library'
    id 'maven-publish'
	id 'signing'
	id "com.diffplug.gradle.spotless" version "4.3.0"
}

group = 'com.formkiq'
version = '1.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    testImplementation group: 'junit', name: 'junit', version:'4.+'
}

java {
    withJavadocJar()
    withSourcesJar()
}

spotless {
  java {
    googleJavaFormat()
  }
}

publishing {
	publications {
	    mavenJava(MavenPublication) {
	    	artifactId = 'lambda-runtime-graalvm'
	        from components.java
	        
	        pom {
                name = 'FormKiQ Lambda Runtime Graalvm'
                description = 'Lambda Runtime Graalvm'
                url = 'https://github.com/formkiq/lambda-runtime-graalvm'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mfriesen'
                        name = 'Mike Friesen'
                        email = 'mike@formkiq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/formkiq/lambda-runtime-graalvm.git'
                    developerConnection = 'scm:git:ssh://github.com/formkiq/lambda-runtime-graalvm.git'
                    url = 'https://github.com/formkiq/lambda-runtime-graalvm.git'
                }
            }
	    }
	}
	repositories {
		maven {
		  credentials {
		    username project.repoUser
		    password project.repoPassword
		  }
		  url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
		}
	}
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifacts {
    archives jar
	
    archives javadocJar
    archives sourcesJar
}

signing {
    sign publishing.publications.mavenJava
}
